#!/usr/bin/env racket
;; -*- scheme  -*-
#lang racket

(require "common.rkt"
         "compiler/compile.rkt"
         "compiler/assemble.rkt"
         "compiler/bootstream.rkt"
         "compiler/disassemble.rkt")

(define print-bootstream? (make-parameter #f))
(define symbols? (make-parameter #f))
(define pretty? (make-parameter #f))
(define count? (make-parameter #f))
(define hex? (make-parameter #f))

(define input-file
  (command-line
   #:once-each
   [("-b" "--bootstream") "print bootstream"
    (print-bootstream? #t)]
   [("-s" "--symbols") "include symboltable"
    (symbols? #t)]
   [("-p" "--pretty") "print in human readable"
    (pretty? #t)]
   [("-c" "--count") "count ram usage"
    (count? #t)]
   [("-x" "--hex") "print numbers in hexadecimal format"
    (hex? #t)]
   #:args (filename)
   filename))

(define (compiled->json compiled)
  (comma-join
   (for/list ((node (compiled-nodes compiled)))
     (format "'~a' : [~a]"
             (node-coord node)
             (comma-join (let ((mem (node-mem node)))
                           (for/list ((i (range (node-len node))))
                             (let ((word (vector-ref mem i)))
                               (if (vector? word)
                                   (format "[~a]"
                                           (comma-join (for/list ((w word))
                                                         (format "'~a'" w))))
                                   word)))))))))

(define (boot-descriptors->json compiled)
  (comma-join
   (for/list ((node (compiled-nodes compiled)))
     (format " '~a' : {~a}"
             (node-coord node)
             (comma-join (list (format "'a' : ~a" (or (node-a node) "None"))
                               (format "'b' : ~a" (or (node-b node) "None"))
                               (format "'io' : ~a"(or (node-io node) "None"))
                               (format "'p' : ~a" (or (node-p node) "None"))
                               (format "\n'stack' : ~a \n"
                                       (if (node-stack node)
                                           (format "[~a]"
                                                   (comma-join (node-stack node)))
                                           "None"))))))))
(define (symbols->json compiled)
  (let ((syms '())
        (symbols #f))
    (for/list ((node (compiled-nodes compiled)))
      (set! symbols (node-symbols node))
      (unless (null? symbols)
        (push syms
              (format "'~a' : {~a}"
                      (node-coord node)
                      (comma-join
                       (for/list ((sym (node-symbols node)))
                         (format "'~a' : {'address' : ~a, 'line' : ~a, 'col' : ~a}"
                                 (symbol-name sym) (symbol-address sym)
                                 (symbol-line sym) (symbol-col sym))))))))
    (comma-join syms)))

(define (assembled->json assembled)
  (comma-join (for/list ((node (compiled-nodes assembled)))
                (format "'~a' : [~a]"
                        (node-coord node)
                        (comma-join (let ((mem (node-mem node)))
                                      (for/list ((i (range (node-len node))))
                                        (vector-ref mem i))))))))


(define (print-json)
  (define compiled (compile (file->string input-file)))
  (define compiled-json (compiled->json compiled))
  (define boot-descriptors-json (boot-descriptors->json compiled))
  (define symbols-json (symbols->json compiled))
  (define assembled (assemble compiled))
  (define assembled-json (assembled->json assembled))

  (define bootstream (sget-convert (make-bootstream assembled)))

  (printf "{~a}\n"
          (comma-join
           (list (format "'file' : '~a'\n" input-file)
                 (format "'compiled': {~a}\n" compiled-json)
                 (format "'boot-descriptors' : {~a}\n" boot-descriptors-json)
                 (format "'symbols': {~a}\n" symbols-json)
                 (format "'assembled': {~a}\n" assembled-json)
                 (format "'bootstream' : [~a] " (comma-join bootstream))
                 ))))

(define (print-count)
  (define compiled (compile (file->string input-file)))
  (define total 0)
  (define (percent a b)
    (exact->inexact (* (/ a b) 100)))
  (for ((n (compiled-nodes compiled)))
    (printf "~a  ~a~a ~a%\n"
            (node-coord n)
            (node-len n)
            (if (> (node-len n) 64) "*" " ")
            (percent (node-len n) 64))
    (set! total (+ total (node-len n))))
  (printf "Total: ~a nodes, ~a words, ~a%\n"
          (length (compiled-nodes compiled)) total (percent total (* 64 144))))

(define (print-pretty)
  (define compiled (compile (file->string input-file)))
  (define compiled-hash (make-hash))
  (for ((node compiled))
    (hash-set! compiled-hash (node-coord node)
               (vector-copy (node-mem node))))

  (define assembled (assemble compiled))

  (define i 0)
  (define name #f)


  (define (pad-print thing [pad 20])
    (let* ((s (format "~a" thing))
           (len (string-length s))
           (str (string-append s (make-string (- pad len) #\ ))))
      (printf str)))

  (define (make-pretty thing)
    (if (vector? thing)
        (vector->list thing)
        thing))

  (define (make-symbol-hash syms)
    (let ((ht (make-hash)))
      (for ((sym syms))
        (hash-set! ht (symbol-address sym) (symbol-name sym) ))
      ht))

  (define (get-name ht index)
    (if (hash-has-key? ht index)
        (hash-ref ht index)
        #f))

  (set! hex? (hex?))
  (define (n val)
    (format (if hex? "~x" "~a") val))

  (for ((node assembled))
    (define coord (node-coord node))
    (define symbols (make-symbol-hash (node-symbols node)))
    (define comp (hash-ref compiled-hash coord))
    (define asm (node-mem node))
    (define word #f)
    (printf "\n\n__________________ node ~a ____________________\n" coord)
    (printf "P = ~a\n" (n (or (node-p node) 0)))
    (printf "     Compiled            Assembled    Disassembled\n")
    (for ((i 64))
      (set! word (vector-ref comp i))
      (unless (equal? word (vector #f #f #f #f))
        (set! name (get-name symbols i))
        (when name (printf "~a:\n" name))
        (printf "~a    " (n i))
        (pad-print (make-pretty word))
        (pad-print (format "~a" (n (vector-ref asm i))) 13)
        (printf "~a\n" (make-pretty (disassemble-word (vector-ref asm i))))))))

(if (count?)
    (print-count)
    (if (pretty?)
        (print-pretty)
        (print-json)))
